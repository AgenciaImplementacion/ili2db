group 'ch.interlis'
version '3.12.2'+System.getProperty('release','-SNAPSHOT')

apply plugin: "java"
apply plugin: "maven"

configurations {
	ili2db
	ili2pgCompileClasspath {
		extendsFrom implementation
	}
	ili2pgRuntimeClasspath {
		extendsFrom runtimeOnly
		extendsFrom ili2pgCompileClasspath
	}
	ili2pgTestCompileClasspath {
		extendsFrom testImplementation
		extendsFrom ili2pgCompileClasspath
	}
    deployerJars
}

sourceCompatibility = "1.6" 
targetCompatibility = "1.6"

// to get the latest SNAPSHOT uncomment the following lines
//configurations.all {
    // check for updates every build
//    resolutionStrategy.cacheChangingModulesFor 0, 'seconds'
//}

dependencies {
    implementation group: 'ch.interlis', name: 'iox-ili', version: '1.20.7'
    implementation group: 'ch.interlis', name: 'ili2c-tool', version: "4.7.9"
    implementation group: 'ch.ehi', name: 'ehisqlgen', version: "1.13.+"
    testImplementation group: 'junit', name: 'junit', version: '4.12'
	testImplementation group: 'org.hamcrest', name: 'hamcrest-library', version: '1.3'
	ili2pgCompileClasspath group: 'org.postgresql', name: 'postgresql', version: '42.1.4.jre6'
	ili2db group: project.group, name: project.name, version: project.version
    deployerJars "org.apache.maven.wagon:wagon-ftp:3.0.0"
}

repositories {
    mavenLocal()
    maven {
        url "http://jars.interlis.ch"
    }
    mavenCentral()
}

Properties properties = new Properties()
File propFile=project.rootProject.file('user.properties')
if(propFile.exists()){
	properties.load(propFile.newDataInputStream())
}
def git = System.getProperty('git',properties.get('git','git'))
def repos_pwd = System.getProperty('repos_pwd',properties.get('repos_pwd','repos_pwd'))
def repos_usr = System.getProperty('repos_usr',properties.get('repos_usr','repos_usr'))
def dburl = System.getProperty('dburl',properties.get('dburl'))
def dbusr = System.getProperty('dbusr',properties.get('dbusr'))
def dbpwd = System.getProperty('dbpwd',properties.get('dbpwd'))
def python= System.getProperty('python',properties.get('python','python'))
def rst2html= System.getProperty('rst2html',properties.get('rst2html','rst2html'))
    
def generatedResources = "$buildDir/generated-resources/main"

def getGitHash = { ->
    def stdout = new ByteArrayOutputStream()
    exec {
        commandLine git, 'rev-parse', 'HEAD'
        standardOutput = stdout
    }
    return stdout.toString().trim()
}

sourceSets {
  main {
    //let's register an output folder on the main SourceSet:
    //it is now a part of the 'main' classpath and will be a part of the jar
    output.dir(generatedResources, builtBy: 'generateMyResources')
    java {
    	srcDirs=['src']
    }
  }
  ili2pg {
    java {
    	srcDirs=['ili2pg/src']
    }
    compileClasspath += sourceSets.main.output
  }
  ili2pgTest {
    java {
    	srcDirs=['ili2pg/test/java']
    }
    //compileClasspath = [ configurations.ili2pgCompileClasspath, configurations.testImplementation]
    compileClasspath += sourceSets.main.output
    compileClasspath += sourceSets.ili2pg.output
    runtimeClasspath += sourceSets.main.output
    runtimeClasspath += sourceSets.ili2pg.output
  }
  test {
  	  java {
  	  	  srcDirs=['test/java']
  	  }
  }
}
task printcp {
	doLast{
		//sourceSets.main.output.each { println it }
		// components.java.each { println it }
		System.out.println components.java
	}
}
task usrdoc(type:Exec) {
	def infile=new File(project.projectDir,'docs/ili2db.rst')
	def outfile=new File('docs/ili2db.html')
	inputs.file infile
	outputs.file outfile
	doFirst{
		new File(project.buildDir,'docs').mkdir()
	}
		workingDir = project.buildDir
		executable python
		args = [rst2html, infile, outfile]
}
task ili2pgJar(dependsOn: jar,type: Jar) {
	baseName = 'ili2pg'
    from sourceSets.ili2pg.output
  manifest {
    attributes(
      "Main-Class": "ch.ehi.ili2pg.PgMain",
      "Class-Path": 'libs/'+jar.archiveName+' '+configurations.ili2pgRuntimeClasspath.collect { 'libs/'+it.getName() }.join(' '))
  }   
}
task ili2pgBindist(dependsOn:[usrdoc,ili2pgJar], type: Zip){
	baseName = 'ili2pg'
    classifier 'bindist'
	destinationDir = file('dist')
	from ili2pgJar
	into('docs'){
		from files(fileTree("docs").include("LICENSE.*"),"docs/README.txt","docs/CHANGELOG.txt","build/docs/ili2db.html")
	}
	into('libs'){
		from configurations.ili2pgRuntimeClasspath
		from jar
		//def jars=[]
		//subprojects.each {
        //	jars+=it.libsDir
        //}
        //from jars
	}
	// version = '1.0.6'
}
task ili2pgTest(type: Test) {
	maxParallelForks = 1
	forkEvery = 1
    testClassesDirs = sourceSets.ili2pgTest.output.classesDirs
    classpath = configurations.ili2pgTestCompileClasspath
    classpath += sourceSets.main.output
    classpath += sourceSets.ili2pg.output
    classpath += sourceSets.ili2pgTest.output 
	systemProperty 'dburl', dburl
	systemProperty 'dbusr', dbusr
	systemProperty 'dbpwd', dbpwd
    testLogging.exceptionFormat = 'full'
}

task generateMyResources {
	doLast {
		def versionProps = new Properties()
		versionProps.setProperty('version', "$project.version")
		versionProps.setProperty('versionCommit', getGitHash())
		def versionPropsFile = new File(generatedResources,"ch/ehi/ili2db/Version.properties")
		versionPropsFile.getParentFile().mkdirs();
		versionProps.store(versionPropsFile.newWriter(), null);
	}
}

test {
	// when Gradle forks a new Java process, it does not automatically pass the 
	// environment variable values along to the new environment. One has to 
	// explicitly pass these variables
	systemProperty 'dburl', dburl
	systemProperty 'dbusr', dbusr
	systemProperty 'dbpwd', dbpwd
    testLogging.exceptionFormat = 'full'
}

configurations {
    antClasspath
}

dependencies {
    antClasspath group: 'org.apache.ant', name: 'ant-junit', version: '1.9.13'

}

ClassLoader antClassLoader = org.apache.tools.ant.Project.class.classLoader
 configurations.antClasspath.each { File f ->
   antClassLoader.addURL(f.toURI().toURL())
 
}
task testreport {
	doLast{
		//ant.taskdef(name: 'antClasspath',
        //            classname: 'org.apache.tools.ant.taskdefs.optional.junit.XMLResultAggregator',
        //            classpath: configurations.antClasspath.asPath)
		ant.delete(dir:'build/junitreport', quiet:'true')
		ant.mkdir(dir:'build/junitreport')
		ant.junitreport(todir: 'build/junitreport/', tofile: 'ceis.xml'){
			fileset(dir:"build/test-results"){
				include(name: "**/TEST-*.xml")
			}
			report(todir: 'build/junitreport',format: 'noframes', styledir: 'travis'){
			}
		}
	}
}

// use plugin 'maven' instead of 'maven-publish', 
// because 'maven-publish' doesn't support ftp repository access
artifacts {
    archives(jar.archivePath){
    	builtBy jar
    }
	archives(ili2pgBindist.archivePath) {
        type 'zip'
        classifier 'bindist'
        builtBy ili2pgBindist
    }
    archives(ili2pgJar.archivePath){
    	builtBy ili2pgJar
    }
}

install {
    repositories {
    	mavenInstaller {
            addFilter('ili2pg') {artifact, file ->
                artifact.name == 'ili2pg'
            }            
            addFilter('ili2db') {artifact, file ->
                artifact.name == 'ili2db'
            }            
            pom('ili2pg').artifactId = 'ili2pg'
            pom('ili2pg').scopeMappings.with {
                mappings.clear()
                addMapping(300, configurations.ili2pgCompileClasspath, 'runtime')
                addMapping(300, configurations.ili2db, 'runtime')
            }
            pom('ili2db').artifactId = 'ili2db'
            pom('ili2db').scopeMappings.with {
                mappings.clear()
                addMapping(300, configurations.implementation, 'runtime')
            }
    	}
    }
}

uploadArchives {
    repositories {
        mavenDeployer {
        	configuration = configurations.deployerJars
			repository(url: 'ftp://ftp.interlis.ch'){
            	authentication(userName: repos_usr, password: repos_pwd)
            }
            addFilter('ili2pg') {artifact, file ->
                artifact.name == 'ili2pg'
            }            
            addFilter('ili2db') {artifact, file ->
                artifact.name == 'ili2db'
            }            
            pom('ili2pg').artifactId = 'ili2pg'
            pom('ili2pg').scopeMappings.with {
                mappings.clear()
                addMapping(300, configurations.ili2pgCompileClasspath, 'runtime')
                addMapping(300, configurations.ili2db, 'runtime')
            }
            pom('ili2db').artifactId = 'ili2db'
            pom('ili2db').scopeMappings.with {
                mappings.clear()
                addMapping(300, configurations.implementation, 'runtime')
            }
        }
    }
}